########################################################################
###
### cshrc to set local environment for CODE
###
### environment specific to running the ORPG or compiling ORPG software
### is created at the end of this file
###
########################################################################

###
### path for applications installed in /opt (if any) is set elsewhere
###

set path=(/bin               \
          /usr/bin           \
          /usr/local/bin     \
          /usr/X11R6/bin     \
          /usr/sbin          \
          /sbin              \
         .) 
 
setenv MANPATH /usr/share/man:/usr/local/man:/usr/local/share/man:/usr/X11R6/man
setenv LD_LIBRARY_PATH /usr/lib:/usr/local/lib:/usr/X11R6/lib

set prompt = "${user}:${cwd}:  \!>"

set	history=128				#long short-term memory
set	savehist=64				#short long-term memory

### DO NOT SET noclobber, ORPG does not launch if set
#set     noclobber				#prevent Russian roulette

set     ignoreeof				#for the ^D trigger-happy
set	filec					#file completion with ESC
set	notify					#of job completion


# preferred for ORPG
### WARNING: setting umask to other values can prevent some
###          ORPG / CODE installation procedures to fail
umask	022					#default file permissions mask

#=-=-=-=-=-=-=-=-=-=-> Utilities <-=-=-=-=-=-=-=-=-=-=#

alias	cp	'cp -i'				# be safe and sure
alias	mv	'mv -i'				# be safe and sure
alias	pwd	'echo $cwd'			# faster pwd

alias	cd	'cd \!*;	set prompt = "${user}:`basename $cwd`/	\! > "'
alias	pushd	'pushd \!*;	set prompt = "${user}:`basename $cwd`/	\! > "'
alias	popd	'popd \!*;	set prompt = "${user}:`basename $cwd`/	\! > "'

####################   CODE-specific modifications   ########################

### AR2_DIR sets the default location of Archive II disk files to be used
#   as input to the ORPG when using the ORPG 'play_a2' utility
### NOTE: To provide initial input data, several Archive II data files are 
#         placed into the following directory as part of the CODE installation.
#         If desired, select another location for additional files and
#         modify the variable as required.
setenv AR2_DIR $HOME/ar2data

### An environmental variable is used to reference the location of the 
#   default CVG preference files.  
### NOTE: The following directory reflects the initial installation
#         location for these files.  If a global installation script
#         is run, the location should be changed accordingly.
## FOR CVG 6.2 and later, the following variable is used. Note that it does 
## not include the subdirectory named 'cvgN.n'.  There is no reason to change
## this variable when installing a new version of CVG.
## It can be modified to support special global locations of the default files
setenv CVG_DEF_PREF_DIR $HOME/tools


### CV_ORPG_BUILD is used to permit the newest versions of CVT to function 
#   correctly on previous ORPG builds.  It should always be set to the ORPG
#   major build number (e.g., 6, 7, etc.) of the ORPG that produced the 
#   linear buffer or single product binary file being used.  
#
#   CVG has a build number option menu and only uses this variable to set
#   the initial build when CVG is launched. 
#
#   If the wrong build number is used incorrect elevation numbers may be 
#   displayed or the CVG product list may not be sorted correctly.
setenv CV_ORPG_BUILD 14

######################## ORPG-specific settings ###############################

### orpg_env_cshrc creates the proper c shell environment for running ORPG software
source $HOME/orpg_env_cshrc

### build_env_cshrc creates the proper c shell environment for building ORPG software
source $HOME/build_env_cshrc

