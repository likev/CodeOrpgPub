C
C RCS info
C $Author: christie $
C $Locker:  $
C $Date: 2002/12/03 21:44:56 $
C $Id: a3078e.ftn,v 1.3 2002/12/03 21:44:56 christie Exp $
C $Revision: 1.3 $
C $State: Exp $
C
      SUBROUTINE A3078E__NORTH_HEMI (RADAZM,I, MATCH,HL,HR,
     $                               SLANT_LEFT,SLANT_RIGHT,SLOPE)
*.********************************************************************
*.                    M O D U L E  P R O L O G U E
*.
*.  MODULE NAME: A3078E__NORTH_HEMI
*.
*.  MODULE VERSION: 0011
*.
*.  MODULE LANGUAGE: FORTRAN
*.
*.  CHANGE HISTORY:
*.
*.       DATE          VERSION   PROGRAMMER           NOTES
*.       ----------    -------   ----------------     --------------------
*.       03 03 89      0000      CHARLIE BAUMHER      SPR # 80271
*.       05 08 90      0001      CHI CHAN             SPR # 90697
*.       11 03 90      0002      MARK FRESCH          SPR # 91248
*.       05 13 91      0002      PAUL JENDROWSKI      SPR # 92133
*.       12 03 91      0003      STEVE ANDERSON       SPR # 92740
*.       12/10/91      0004      ED NICHLAS           SPR 92637 PDL Removal
*.       04/20/92      0005      Toolset              SPR 91895
*.       03/25/93      0006      Toolset              SPR NA93-06801
*.       01/28/94      0007      Toolset              SPR NA94-01101
*.       03/03/94      0008      Toolset              SPR NA94-05501
*.       04/11/96      0009      Toolset              CCR NA95-11802
*.       12/23/96      0010      Toolset              CCR NA95-11807
*.       03/16/99      0011      Toolset              CCR NA98-23803
*.
*.  CALLING SEQUENCE:
*.  CALL A3078E__NORTH_HEMI(RADAZM,I, MATCH,HL,HR,
*.                          SLANT_LEFT,SLANT_RIGHT,SLOPE)
*.
*.  MODULE FUNCTION:
*.   THIS VERTICAL CROSS SECTION ROUTINE TAKES IN A RADIAL OF DATA
*.  AND DETERMINES IF THE RADIAL IS BETWEEN POINTS P1 & P2. IF
*.  THE RADIAL MATCHES THEN WHERE AND HOW IT INTERSECTS THE LINE P1P2
*.  IS DETERMINED.  THIS IS THE NORTH_HEMI CASE SO THETA3 IS AT P1 AND
*.  P1 IS THE LEFTMOST POINT AND P2 IS THE RIGHTMOST POINT.  DISTANCES
*.  ALONG P1P2 INCREASE FROM P1 (LEFT TO RIGHT) AND HR SHOULD BE
*.  GREATER THAN HL.  EACH NEW RADIAL SHOULD RESULT IN LARGER HL, HR.
*.
*.  MODULES CALLED: NONE.
*.
*.  PARAMETERS:          (*:  G = GLOBAL, C = COMMON, P = PASSED)
*.
*.    *   INPUT       TYPE        DESCRIPTION
*.    -   -----       ----        -----------
*.    C   BEG_AZ      R*4         Beginning azimuth of P1-P2 Plane.
*.    C   END_AZ      R*4         Ending azimuth of P1-P2 plane.
*.    C   ORIG_PTS    R*4         Table of cross-setion end points for each
*.                                request.
*.    C   P1P2_LENG   R*4         Length of P1-P2 plane.
*.    C   THETA3      R*4         Azimuth angle between P1-P2 line and 0-P1
*.                                (the baseline).
*.    C   ZEROCROSS   L*4         Set if P1-P2 plan crosses 360.
*.    C   DEGTORAD    R*4         (CONSTANT)Constant, used in calculation to
*.                                convert degrees to radians.
*.    C   P1_AZZ      I*4         (CONSTANT)Constant used as an index into the
*.                                buffer ORIG_PTS for P1's Azimuth.
*.    C   P1_RAN      I*4         (CONSTANT)Constant used as an index into the
*.                                buffer ORIG_PTS for P1's Range.
*.    C   P2_AZZ      I*4         (CONSTANT)Constant used as an index into the
*.                                buffer ORIG_PTS for P2's Azimuth.
*.    C   P2_RAN      I*4         (CONSTANT)Constant used as an index into the
*.                                buffer ORIG_PTS for P2's Range.
*.    C   TWO         I*4         (CONSTANT)Constant value of an integer 2.
*.    C   ZERO        I*4         (CONSTANT)Program parameter for 0.
*.    P   HL          R*4         DIST FROM P1 TO LEFT_AZ ALONG THE P1P2 LINE
*.    P   HR          R*4         DIST FROM P1 TO RIGHT_AZ ALONG THE P1P2 LINE
*.    P   I           I*4         Product requested index.
*.    P   MATCH       L*4         INCOMING AZ MATCHES P1P2 RANGE
*.    P   RADAZM      R*4         AZIMUTH OF THIS BASE RADIAL
*.    P   SLANT_LEFT  R*4         RANGE FROM RADAR TO LEFT_AZ
*.    P   SLANT_RIGHT R*4         RANGE FROM RADAR TO RIGHT_AZ
*.
*.    *   OUTPUT      TYPE        DESCRIPTION
*.    -   ------      ----        -----------
*.    P   HL          R*4         DIST FROM P1 TO LEFT_AZ ALONG THE P1P2 LINE
*.    P   HR          R*4         DIST FROM P1 TO RIGHT_AZ ALONG THE P1P2 LINE
*.    P   MATCH       L*4         INCOMING AZ MATCHES P1P2 RANGE
*.    P   SLANT_LEFT  R*4         RANGE FROM RADAR TO LEFT_AZ
*.    P   SLANT_RIGHT R*4         RANGE FROM RADAR TO RIGHT_AZ
*.    P   SLOPE       R*4         SLOPE OF RADIAL THRU P1P2 PLANE
*.
*.    *   ACTUAL ARGUMENTS  TYPE        DESCRIPTION
*.    -   ----------------  ----        -----------
*.
*.  DATABASE/FILE REFERENCE:  None
*.
*.  INTERNAL TABLES/WORK AREA:
*.
*.    NAME        TYPE        DESCRIPTION
*.    ----        ----        -----------
*.    ALPHA_LEFT  R*4         ANGLE BETWEEN SLANT_LEFT LINE & LINE P1P2
*.    ALPHA_RIGHT R*4         ANGLE BETWEEN SLANT_RIGHT LINE & LINE P1P2
*.    AZ1         R*4         AZIMUTH OF POINT 1
*.    AZ2         R*4         AZIMUTH OF POINT 2
*.    LEFT_AZ     R*4         INCOMING RADIAL - 0.5 TOLERANCE
*.    PI          R*4         180 DEGREES
*.    RIGHT_AZ    R*4         INCOMING AZIMUTH + TOLERANCE
*.    RINIT       R*4         CONSTANT VALUE OF 0.0
*.    THETA_LEFT  R*4         ANGLE BETWEEN LEFT_AZ RADIAL AND
*.    THETA_RIGHT R*4         ANGLE BETWEEN RIGHT_AZ RADIAL
*.    TOLERANCE   R*4         RANGE FOR MATCHING AZIMUTHS.
*.    TWOPI       R*4         CONSTANT VALUE OF 360.0
*.
*.  GLOBAL BLOCKS REFERENCED:
*.
*.
*.  COMMON BLOCKS REFERENCED:
*.
*.    A3078CA
*.
*.  ERROR CONDITIONS: NONE
*.
*.  ASSUMPTIONS/RESTRICTIONS: NONE
*.
*.  DEVIATION FROM STANDARDS: NONE
*.
*.  COMPILATION INSTRUCTIONS:
*.
*.        THIS MODULE IS COMPILED USING THE COMP07.CSS
*.
*.  LINKAGE INSTRUCTIONS:
*.
*.        THIS MODULE IS LINKED USING THE LINK07.CSS
*.
*.  MISC: NONE
*.
*.*******************************************************************
C***************************************************************
C*
C*                A 3 0 7 8 E __ NORTH_HEMI
C*
C*   VERTICAL CROSS SECTION ROUTINE TO CHECK EACH BASE RADIAL
C*   REQUESTED BY THE PUP.
C*
C***************************************************************
C*
C*    MODULE     A3078E__NORTH_HEMI (RADAZM,I, MATCH,HL,HR,
C*                                   SLANT_LEFT,SLANT_RIGHT,SLOPE)
C*
      IMPLICIT NONE
C*
C*  INCLUDE COMMON PARAMETER FILE
C*
$INCLUDE A307.INC,**A30788   (     NLIST)
$INCLUDE          **A3078CA  (     NLIST)
C*
C***************************************************************
C*  LOCAL DECLARATIONS
C*
      INTEGER*4    I
C
      REAL*4       RADAZM, TOLERANCE
      REAL*4       RINIT
      REAL*4       PI, TWOPI
      REAL*4       LEFT_AZ, RIGHT_AZ
      REAL*4       THETA_LEFT, THETA_RIGHT
      REAL*4       ALPHA_LEFT, ALPHA_RIGHT
      REAL*4       SLANT_LEFT, SLANT_RIGHT
      REAL*4       HL, HR, SLOPE
      REAL*4       AZ1, AZ2
C
      LOGICAL      MATCH
C*
C***************************************************************
C*  LOCAL PARAMETER  DECLARATIONS
C*
      PARAMETER   ( PI = 180.0 )
      PARAMETER   ( TWOPI = 360.0 )
      PARAMETER   ( TOLERANCE = .5 )
      PARAMETER   ( RINIT = 0.0 )
C*
C*********************************************************************
C*  CHECK IF THE INCOMING AZIMUTH IS BETWEEN POINTS P1 AND P2
C*   + /- TOLERANCE.
C*
      MATCH = .FALSE.
      IF ( ZEROCROSS(I) ) THEN
        IF (RADAZM.GE.BEG_AZ(I) .OR. RADAZM.LE.END_AZ(I)) MATCH=.TRUE.
      ELSE
        IF (RADAZM.GE.BEG_AZ(I) .AND. RADAZM.LE.END_AZ(I)) MATCH=.TRUE.
      ENDIF
C
      IF ( MATCH ) THEN
C*
C*********************************************************************
C*  CALCULATE THE 2-EDGE AZIMUTHS OF THE INCOMING RADIAL
C*
        LEFT_AZ  = RADAZM - TOLERANCE
        RIGHT_AZ = RADAZM + TOLERANCE
C
        IF ( LEFT_AZ.LT.ZERO ) LEFT_AZ = LEFT_AZ + TWOPI
        IF ( RIGHT_AZ.GE.TWOPI ) RIGHT_AZ = RIGHT_AZ - TWOPI
C
X       TYPE *,'8E: FOUND MATCHING AZ ',RADAZM,', L,R AZ=(',
     $        LEFT_AZ,RIGHT_AZ,'), FOR PRODUCT NUMBER ',I
C*
C*********************************************************************
C*  CALCULATE THE ANGLE BETWEEN THE EDGE AZIMUTHS & THE BASELINE 0-P2
C*
C*  DETERMINE IF THE LEFT AZIMUTH IS IN BETWEEN POINT 1 AND 2
C
        AZ1 = ORIG_PTS(P1_AZZ,I)
        AZ2 = ORIG_PTS(P2_AZZ,I)
        IF ((AZ1.LT.AZ2.AND.LEFT_AZ.GT.AZ1.AND.LEFT_AZ.LE.AZ2) .OR.
     $      (AZ1.GT.AZ2.AND.(LEFT_AZ.GT.AZ1.OR.LEFT_AZ.LE.AZ2))) THEN
C*
C*********************************************************************
C*  NORMAL CASE:
C*  CALCULATE THE SLANT RANGE FROM THE RADAR TO THE P1P2 LINE
C*  ALONG THE LEFT-EDGE OF THE AZIMUTH LINE. CALCULATE THE RANGE FROM
C*  P1 TO THE LEFT_AZIMUTH (HL).
C*
          THETA_LEFT  = LEFT_AZ - ORIG_PTS(P1_AZZ, I)
          IF (THETA_LEFT.LT.RINIT) THETA_LEFT = THETA_LEFT + TWOPI
          ALPHA_LEFT  = PI - THETA3(I) - THETA_LEFT
          SLANT_LEFT  = (ORIG_PTS(P1_RAN,I) * SIN( THETA3(I)
     $                * DEGTORAD)) / SIN( ALPHA_LEFT * DEGTORAD )
C
          HL = ORIG_PTS(P1_RAN,I)**TWO + SLANT_LEFT**TWO - TWO*
     $          ORIG_PTS(P1_RAN,I)*SLANT_LEFT*COS(THETA_LEFT*DEGTORAD)
          IF (HL.LT.RINIT) HL = RINIT
          HL = SQRT( HL )
C
X         TYPE *,'8E: NORMAL LEFT CASE: AZ1=',AZ1,', LAZ=',
     $           LEFT_AZ,', AZ2=',AZ2
CX         TYPE *,'8E: THETA_LEFT = ',THETA_LEFT,'  SLANT LEFT = ',
C     $                  SLANT_LEFT,
C     $             '  ALPHA_LEFT =  ',ALPHA_LEFT
        ELSE
C
C*  SPECIAL CASE OF EDGE AZIMUTH
C
          SLANT_LEFT = ORIG_PTS(P1_RAN,I)
          HL         = ZERO
C
X         TYPE *,'8E: LEFT EDGE AZ = ',LEFT_AZ,', AZ1 = ',AZ1,
     $          ', SL = ',SLANT_LEFT,', HL = ',HL
C
        ENDIF
C
C*********************************************************************
C*  NOW DO THE RIGHT SIDE OF THE AZIMUTH
C
C*  DETERMINE IF THE RIGHT AZIMUTH IS IN BETWEEN POINT 1 AND 2
C
        IF ((AZ1.LT.AZ2.AND.RIGHT_AZ.GE.AZ1.AND.RIGHT_AZ.LT.AZ2) .OR.
     $      (AZ1.GT.AZ2.AND.(RIGHT_AZ.GE.AZ1.OR.RIGHT_AZ.LT.AZ2))) THEN
C*
C*********************************************************************
C*  NORMAL CASE:
C*  CALCULATE THE SLANT RANGE FROM THE RADAR TO THE P1P2 LINE ALONG
C*  THE RIGHT-EDGE OF THE AZIMUTH LINE (SLANT_RIGHT). CALCULATE
C*  THE HORIZONTAL RANGE FROM P1 TO THE RIGHT AZIMUTH (HR).
C*
          THETA_RIGHT = RIGHT_AZ - ORIG_PTS(P1_AZZ,I)
          IF (THETA_RIGHT.LT.RINIT) THETA_RIGHT = THETA_RIGHT + TWOPI
          ALPHA_RIGHT = PI - THETA3(I) - THETA_RIGHT
          SLANT_RIGHT = (ORIG_PTS(P1_RAN,I) * SIN( THETA3(I)
     $                * DEGTORAD)) / SIN( ALPHA_RIGHT * DEGTORAD )
C
          HR = ORIG_PTS(P1_RAN,I)**TWO + SLANT_RIGHT**TWO - TWO*
     $        ORIG_PTS(P1_RAN,I)*SLANT_RIGHT*COS(THETA_RIGHT*DEGTORAD)
          IF ( HR.LT.RINIT ) HR = RINIT
          HR = SQRT( HR )
C
X         TYPE *,'8E: NORMAL RIGHT CASE: AZ1=',AZ1,', RAZ=',
     $           RIGHT_AZ,', AZ2=',AZ2
CX         TYPE *,'8E: THETA_RIGHT = ',THETA_RIGHT,'  SLANT RIGHT = ',
C     $                    SLANT_RIGHT,
C     $             '  ALPHA_RIGHT = ',ALPHA_RIGHT
        ELSE
C
C*  SPECIAL CASE OF EDGE AZIMUTH
C
          SLANT_RIGHT = ORIG_PTS(P2_RAN,I)
          HR          = P1P2_LENG(I)
C
X         TYPE *,'8E: RIGHT EDGE AZ = ',RIGHT_AZ,', AZ2 = ',AZ2,
     $          ', SR = ',SLANT_RIGHT,', HR = ',HR
C
        ENDIF
C
CX       TYPE *,'8E: SLANT_LEFT  SLANT_RIGHT =',SLANT_LEFT,SLANT_RIGHT,
C     $         '  HL, HR = ',HL, HR
C*
C*********************************************************************
C*  CALCULATE THE SLOPE OF THE BEAM THROUGH THE PLANE
C*
        IF ( HR - HL .LE. ZERO ) THEN
          SLOPE = ZERO
        ELSE
          SLOPE = ( SLANT_RIGHT - SLANT_LEFT )  /  ( HR - HL )
        ENDIF
C
CX       TYPE *,'8E: SLOPE = ',SLOPE
C
      ENDIF   ;MATCH IS TRUE
      RETURN
      END
