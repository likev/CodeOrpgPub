C
C RCS info
C $Author: christie $
C $Locker:  $
C $Date: 2002/12/16 19:40:42 $
C $Id: a315i5.ftn,v 1.3 2002/12/16 19:40:42 christie Exp $
C $Revision: 1.3 $
C $State: Exp $
C
      SUBROUTINE A315I5__MERGE_CELLS( )
*.********************************************************************
*.                    M O D U L E  P R O L O G U E
*.
*.  MODULE NAME: A315I5__MERGE_CELLS
*.
*.  MODULE VERSION: 0002
*.
*.  MODULE LANGUAGE: FORTRAN
*.
*.  CHANGE HISTORY:
*.
*.       DATE          VERSION   PROGRAMMER           NOTES
*.       ----------    -------   ----------------     ---------------
*.       06 28 95      0000      STEVE SMITH          CCR NA94-26302
*.       12/23/96      0001      Toolset              CCR NA95-11807
*.       03/16/99      0002      Toolset              CCR NA98-23803
*.
*.  CALLING SEQUENCE: CALL A315I5__MERGE_CELLS( )
*.
*.  MODULE FUNCTION:
*.
*.
*.  MODULES CALLED: A315J5__COMB_CELL_ATTR 
*.
*.  PARAMETERS:          (*:  G = GLOBAL, C = COMMON, P = PASSED)
*.
*.    *   INPUT        TYPE        DESCRIPTION
*.    -   -----        ----        -----------
*.    G   ADPELMRG     R*4         Adaptation data Elevation Merge Threshold
*.                                 stored in Storm Data.
*.    G   ADPHTMRG     R*4         Adapatation data Height Merge Threshold
*.                                 stored in Storm Data
*.    C   ADPHZMRG_SQR R*4         Square of the horizontal merge distance
*.                                 adaptable parameter (km**2). Used to merge
*.                                 storm cells from different elevations.
*.    C   CELLCOMP     R*4         Array of potential storm cell
*.                                 characteristics.
*.    C   NBR_COMP     I*4         Number of components that comprise each
*.                                 potential storm cell.
*.    C   NBR_DEL_CELL I*4         Number of potential storm cells deleted by
*.                                 merges and proximity checks.  Although
*.                                 tallied it is not used.
*.    C   NBR_STMS     I*4         Counter for the number of storm cells
*.                                 constructed from components before calls to
*.                                 merge or delete modules.
*.    C   STRMCELL     R*4         Array of storm cell characteristics of all
*.                                 storm cells built from components.
*.    C   CCM_ELV      I*4         (CONSTANT)Location in the CELLCOMP array of
*.                                 the average elevation (degrees) of the scan
*.                                 from which a component was built.
*.    C   NCELLCHR     I*4         (CONSTANT)Number of characteristics (17) of
*.                                 components that are assigned to a storm
*.                                 cell.
*.    C   STC_BAS      I*4         (CONSTANT)Location in the STRMCELL array of
*.                                 the height (km ARL) of the the lowest
*.                                 centroid of any component associated with a
*.                                 storm cell.
*.    C   STC_MWA      I*4         (CONSTANT)Location in the STRMCELL array of
*.                                 mass-weighted area of a storm cell.
*.    C   STC_TOP      I*4         (CONSTANT)Location in the STRMCELL array of
*.                                 the height (km ARL) of the highest centroid
*.                                 of any component associated with a storm
*.                                 cell.
*.    C   STC_XCN      I*4         (CONSTANT)Location in the STRMCELL array of
*.                                 the mass-weighted X-coordinate (km) of a
*.                                 storm cell's 3-D centroid.
*.    C   STC_YCN      I*4         (CONSTANT)Location in the STRMCELL array of
*.                                 the mass-weighted Y coordinate (km) of a
*.                                 storm cell's 3-D centroid.
*.
*.    *   OUTPUT       TYPE        DESCRIPTION
*.    -   ------       ----        -----------
*.    G   ADPELMRG     R*4         Adaptation data Elevation Merge Threshold
*.                                 stored in Storm Data.
*.    G   ADPHTMRG     R*4         Adapatation data Height Merge Threshold
*.                                 stored in Storm Data
*.    C   CELLCOMP     R*4         Array of potential storm cell
*.                                 characteristics.
*.    C   NBR_COMP     I*4         Number of components that comprise each
*.                                 potential storm cell.
*.    C   NBR_DEL_CELL I*4         Number of potential storm cells deleted by
*.                                 merges and proximity checks.  Although
*.                                 tallied it is not used.
*.
*.    *   ACTUAL ARGUMENTS  TYPE        DESCRIPTION
*.    -   ----------------  ----        -----------
*.
*.  DATABASE/FILE REFERENCE:  None
*.
*.  INTERNAL TABLES/WORK AREA:
*.
*.    NAME      TYPE        DESCRIPTION
*.    ----      ----        -----------
*.    CHR_NDX   I*4         Do loop index for number of cell characteristics
*.    CMP_NDX   I*4         Do loop variable for number of cell componets
*.    DELTA_EL  R*4         Elevation angle difference
*.    DELTA_HT  R*4         Height Difference
*.    HORZDIST2 R*4         Centroid Distance Difference
*.    NCMPMAX   I*4         Maximum number of components (20) per storm cell
*.                          or one component per elevation scan.
*.    NCOMP     I*4         Number of componets
*.    NEWCOMP   I*4         Number of componets from storm cell that is being
*.                          merged
*.    STINX     I*4         Do loop variable for second storm cell index
*.    STM_NDX   I*4         Do loop variable for the number of storm cells
*.    XDIST     R*4         X coordinate distance between two storm cells
*.    YDIST     R*4         Y coordinate distance between two storm cells
*.
*.  GLOBAL BLOCKS REFERENCED:
*.
*.    A315CSAD
*.
*.  COMMON BLOCKS REFERENCED:
*.
*.    A315CNCC
*.    A315CUCC
*.    A315CVCC
*.    A315CWCC
*.
*.  ERROR CONDITIONS:  None
*.
*.  ASSUMPTIONS/RESTRICTIONS:
*.
*.
*.  DEVIATIONS FROM STANDARD:  None
*.
*.  DEVIATION FROM STANDARDS: NONE.
*.
*.  COMPILATION INSTRUCTIONS:
*.
*.     Compiled using COMP15.CSS
*.
*.  LINKAGE INSTRUCTIONS:
*.
*.     Linked using LINK15.CSS
*.
*.  MISC:  None
*.
C********************************************************************
C
C           STORM CELL CENTROIDS: MERGE_CELLS ROUTINE
C
C********************************************************************
C
      IMPLICIT NONE
C
C**Attach output parameter-definition file (of "STORM_CENTROIDS")
$INCLUDE A315BUF.INC/G,**A315P5C (NLIST)
C
$INCLUDE A315BUF.INC/G, **A315PSAD (NLIST)
$INCLUDE                **A315CSAD (NLIST)
C
C**Attach internal common area (CNCC)
$INCLUDE A315.INC/G,**A315CNCC (NLIST)
C
C**Attach internal common area (CUCC)
$INCLUDE A315.INC/G,**A315CUPC (NLIST)
$INCLUDE            **A315CUCC (NLIST)
C
C**Attach internal common area (CVCC)
$INCLUDE A315.INC/G,**A315CVPC (NLIST)
$INCLUDE            **A315CVCC (NLIST)
C
C**Attach internal common area (CWCC)
$INCLUDE A315.INC/G,**A315CWCC (NLIST)
C
C*********************** DATA DECLARATIONS **************************
C
      INTEGER  STM_NDX, STINX, CMP_NDX, CHR_NDX, NEWCOMP, NCOMP
C
      REAL     XDIST, YDIST, HORZDIST2, DELTA_EL, DELTA_HT
C
C****************** BEGIN MAIN PROCESSING SECTION *******************
C
C** Logic sets storm mass to zero if that storm cell is deleted owing
C** to merging and excludes it from all future consideration.
C
      DO 160 STM_NDX = 1, NBR_STMS
         IF ( STRMCELL( STC_MWA, STM_NDX ) .GT. 0.0 ) THEN
            DO 150 STINX = STM_NDX+1, NBR_STMS
               IF ( STRMCELL( STC_MWA, STINX ) .GT. 0.0 ) THEN
C
C** Check against elevation and height merge thresholds.
C
                  NCOMP = NBR_COMP( STM_NDX )
C
C** COMPUTE (ELEVATION DIFFERENCE)
C** It is assumed that the storm cells were built/detected from
C** the lowest to the highest elevation scan.  Therefore, at this
C** point the elevation angle of the lowest component in storm cell
C** STM_NDX will always be less than or equal to the elevation angle
C** of any component in a storm cell detected after it (i.e. STINX).
C** And the base of the first detected storm cell will always be
C** less than or equal to the height of the top of any subsequently
C** detected storm cells
C
                  DELTA_EL = CELLCOMP( CCM_ELV, 1, STINX ) -
     >                       CELLCOMP( CCM_ELV, NCOMP, STM_NDX )
C
C** COMPUTE (HEIGHT DIFFERENCE)
C
                  DELTA_HT = STRMCELL(STC_BAS, STINX ) -
     >                       STRMCELL(STC_TOP, STM_NDX)
C
C** IF ((ELEVATION DIFFERENCE is greater than zero) AND (HEIGHT
C**      DIFFERENCE is greater than zero) AND ((ELEVATION DIFFERENCE
C**      is less than or equal to THRESHOLD (Elevation Merge)) OR
C**      (HEIGHT DIFFERENCE is less than or equal to THRESHOLD
C**      (Height Merge))) AND .....
C
                  IF ( DELTA_EL .GT. 0.0 .AND. DELTA_HT .GT. 0.0
     >               .AND. (DELTA_HT .LE. ADPHTMRG .OR. DELTA_EL .LE.
     >               ADPELMRG) ) THEN
C
C** COMPUTE (CENTROID DISTANCE DIFFERENCE)
C
                    XDIST = STRMCELL( STC_XCN, STM_NDX ) -
     >                      STRMCELL( STC_XCN, STINX )
                    YDIST = STRMCELL( STC_YCN, STM_NDX ) -
     >                      STRMCELL( STC_YCN, STINX )
                    HORZDIST2 = XDIST*XDIST + YDIST*YDIST
C
C** IF (CENTROID DISTANCE DIFFERENCE is less than or equal to
C** THRESHOLD (Horizontal Merge)) THEN ....
C
                    IF ( HORZDIST2 .LE. ADPHZMRG_SQR ) THEN
                      NEWCOMP = 0
C
C** Merge the STORM CELLS by moving the COMPONENTs into one STORM
C** CELL,
C
                      DO 130 CMP_NDX = 1, NBR_COMP( STINX )
                         NEWCOMP = NEWCOMP + 1
                         IF ( CMP_NDX + NCOMP .LE. NCMPMAX ) THEN
                            DO 120 CHR_NDX = 1, NCELLCHR
                              CELLCOMP(CHR_NDX,NCOMP+CMP_NDX,STM_NDX)
     >                              = CELLCOMP(CHR_NDX,CMP_NDX,STINX)
 120                        CONTINUE
                         END IF
 130                  CONTINUE
C
C** adding the NUMBER OF COMPONENTS from one STORM CELL to the other
C** STORM CELL,
C
                      NBR_COMP( STM_NDX ) = NCOMP + NEWCOMP
C
C** Decrement the NUMBER OF STORM CELLS.
C
                      NBR_DEL_CELL = NBR_DEL_CELL + 1
C
C** and deleting the other STORM CELL.
C
                      CALL A315J5__COMB_CELL_ATTR( STM_NDX, STINX )
                    END IF
                  END IF
               END IF
 150        CONTINUE
         END IF
 160  CONTINUE
C
C************** END OF PROCESSING FOR SUBROUTINE ********************
C
      RETURN
      END
