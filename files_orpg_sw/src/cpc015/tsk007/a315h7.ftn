C
C RCS info
C $Author: christie $
C $Locker:  $
C $Date: 2002/12/16 19:40:39 $
C $Id: a315h7.ftn,v 1.3 2002/12/16 19:40:39 christie Exp $
C $Revision: 1.3 $
C $State: Exp $
C
      SUBROUTINE A315H7__PAST_LOCS ( STORMIDTYP, STORMBACK )
*.********************************************************************
*.                    M O D U L E  P R O L O G U E
*.
*.  MODULE NAME: A315H7__PAST_LOCS
*.
*.  MODULE VERSION: 0014
*.
*.  MODULE LANGUAGE: FORTRAN
*.
*.  CHANGE HISTORY:
*.
*.       DATE          VERSION   PROGRAMMER           NOTES
*.       ----------    -------   ----------------     ---------------
*.       08 16 85      0000      DENNIS MILLER        BUILD 2
*.       05 07 86      0001      DENNIS MILLER        SPR # 304
*.       11 15 86      0002      M STEWART            SPR # 60346
*.       11 15 86      0002      M STEWART            SPR # 60650
*.       02 26 87      0003      DENNIS MILLER        SPR # 70106
*.       02 22 91      0004      DENNIS MILLER        SPR # 91256
*.       02 15 91      0004      JOHN DEPHILIP        SPR # 91762
*.       12 03 91      0005      STEVE ANDERSON       SPR # 92740
*.       12/10/91      0006      ED NICHLAS           SPR 92637 PDL Removal
*.       04/24/92      0007      Toolset              SPR 91895
*.       03/25/93      0008      Toolset              SPR NA93-06801
*.       01/28/94      0009      Toolset              SPR NA94-01101
*.       03/03/94      0010      Toolset              SPR NA94-05501
*.       04/01/95      0010      Toolset              CCR NA95-11802
*.       05/30/95      0011      STEVE SMITH          CCR NA94-26302
*.       12/23/96      0013      Toolset              CCR NA95-11807
*.       03/16/99      0014      Toolset              CCR NA98-23803
*.
*.  CALLING SEQUENCE: CALL A315H7__PAST_LOCS( STORMIDTYP,STORMBACK )
*.
*.  MODULE FUNCTION:
*.
*.   This routine extracts past positions of "Continuing" storm
*.  cells.  If the storm is "New", all desired positions of the storm
*.  cell prior to its first recorded position are set to "Undefined".
*.
*.  MODULES CALLED: NONE.
*.
*.  PARAMETERS:          (*:  G = GLOBAL, C = COMMON, P = PASSED)
*.
*.    *   INPUT      TYPE        DESCRIPTION
*.    -   -----      ----        -----------
*.    G   ADPNPVOL   I*4         Adaptation data number of volume scans
*.                               without storms threshold stored in Storm Data
*.    G   NSTF_IDT   I*4         (CONSTANT)The number of attributes for each
*.                               storm cell in the array STORMIDTYP which
*.                               contains Storm ID, storm type, and number of
*.                               volumes tracked.
*.    G   NSTF_MAX   I*4         (CONSTANT)The maximum number of storm cells
*.                               the array STORMIDTYP may hold.  The array
*.                               provides the storm cell Id attribute.
*.    G   STF_TYP    I*4         (CONSTANT)Offset to the cell type in the
*.                               STORMIDTYP part of the tracking/forecast
*.                               output buffer.
*.    G   STF_XB     I*4         (CONSTANT)Offset to the past x locations in
*.                               the STORMBACK part of the tracking/forecast
*.                               output buffer.
*.    G   STF_YB     I*4         (CONSTANT)Offset to the past y locations in
*.                               the STORMBACK part of the tracking/forecast
*.                               output buffer.
*.    G   TYP_CON    I*4         (CONSTANT)Flag that indicates that a storm
*.                               cell has a history of at least two volume
*.                               scans.
*.    G   UNDEF      R*4         (CONSTANT)Value that shows that storm cell
*.                               attributes of motion cannot be calculated
*.                               because there is no storm cell history.
*.    C   CORLTABL   I*4         Correlation table containing IDs to the cells
*.                               for all previous volumes (up to the maximum).
*.    C   IXCUR      I*4         Index to the current volume scan in the
*.                               correlation table
*.    C   IXPRE      I*4         Index to the previous volume scan in the
*.                               correlation table
*.    C   NSTMHIST   I*4         Array of the # of cells/volume scan in
*.                               correlation table.
*.    C   POSITABL   R*4         Table of cell-X and Y centroid positions thru
*.                               the time spanned in the Correlation History
*.                               table.
*.    C   X_POS      I*4         (CONSTANT)Index to the x-coordinate of a
*.                               storm cell, the first attribute, in the
*.                               POSITABLE array.
*.    C   Y_POS      I*4         (CONSTANT)Index to the y-coordinate of a
*.                               storm cell, the second attribute, in the
*.                               POSITABLE array.
*.    P   STORMIDTYP I*4         2-D array of supplementary storm desciption
*.                               information per volume scan, output by
*.                               "FORECAST" algorithm.
*.
*.    *   OUTPUT    TYPE        DESCRIPTION
*.    -   ------    ----        -----------
*.    G   ADPNPVOL  I*4         Adaptation data number of volume scans without
*.                              storms threshold stored in Storm Data
*.    C   IXCUR     I*4         Index to the current volume scan in the
*.                              correlation table
*.    P   STORMBACK R*4         3-D array of past X and Y storm cell positions
*.                              at prior volume scans.
*.
*.    *   ACTUAL ARGUMENTS  TYPE        DESCRIPTION
*.    -   ----------------  ----        -----------
*.
*.  DATABASE/FILE REFERENCE:  None
*.
*.  INTERNAL TABLES/WORK AREA:
*.
*.    NAME     TYPE        DESCRIPTION
*.    ----     ----        -----------
*.    IBEF     I*4         Index of storm within Position-History
*.    IC       I*4         Do-loop index for storms of current volume scan.
*.    IVOLBEF  I*4         Index of volume scan with time just before that of
*.                         desired storm-position display, used in
*.                         interpolation process.
*.    IXBACK   I*4         Index for processing back display times
*.    NB       I*4         Do-loop index for back intervals.
*.    NSTF_BAK I*4         Offset to the number of back positions in STORMBACK
*.                         array
*.    NSTF_MPV I*4         Second dimension of STORMBACK array for the number
*.                         of back display times.
*.
*.  GLOBAL BLOCKS REFERENCED:
*.
*.    A315CSAD
*.
*.  COMMON BLOCKS REFERENCED:
*.
*.    A315C6T
*.
*.  ERROR CONDITIONS:  None
*.
*.  ASSUMPTIONS/RESTRICTIONS:
*.
*.  DEVIATION FROM STANDARDS:  None
*.
*.  COMPILATION INSTRUCTIONS:
*.
*.     Compiled using COMP15.CSS
*.
*.  LINKAGE INSTRUCTIONS:
*.
*.     Linked using LINK15.CSS
*.
*.  MISC:  None
*.
C********************************************************************
C
C            STORM POSITION FORECAST : PAST LOCS  SUBROUTINE
C
C********************************************************************
C
      IMPLICIT NONE
C
C**Attach output parameter-definition file (of "POSITION_FORECAST")
$INCLUDE A315BUF.INC/G,**A315P7F (NLIST)
C
C**Attach local STORMS Adaptation Data parameter file:
$INCLUDE **A315PSAD (NLIST)
C
C**Attach local STORMS Adaptation Data definitions:
$INCLUDE **A315CSAD (NLIST)
C
C**Attach internal common block (of "STORM_TRACK" output variables).
$INCLUDE A315.INC/G,**A315C6T (NLIST)
C
C**Attach internal common block (of "FORECAST" local variables).
$INCLUDE **A315CFF (NLIST)
C
C*********************** DATA DECLARATIONS **************************
C
C** From output buffer:
C
      INTEGER  STORMIDTYP( NSTF_IDT, NSTF_MAX )
      REAL     STORMBACK( NSTF_BAK, NSTF_MPV, NSTF_MAX )
C
C** Local:
C
      INTEGER  NB, IC, IBEF, IVOLBEF, IXBACK
C
C********************* DATA INITIALIZATIONS *************************
C
C******************* BEGIN MAIN PROCESSING SECTION ******************
C
C** Determine previous X & Y positions for each "Continuing" storm
C** cell not preceding earliest known record of storm cell.
C
      DO 1000  IC = 1, NSTMHIST( IXCUR )
C
         IF ( STORMIDTYP(STF_TYP,IC) .EQ. TYP_CON )  THEN
C
C** Initialize volume scan pointer & storm cell index.
C
            IBEF = CORLTABL( IC,IXCUR )
            IVOLBEF = IXPRE
            IXBACK = 0
C
 330        CONTINUE
C
C**  If Correlation Table value is positive, the storm cell existed
C**  at a previous (to IVOLBEF) volume scan.
C
            IF ( CORLTABL( IBEF,IVOLBEF ) .GT. 0 )  THEN
C
C**  Save past position (X & Y).
C
               IXBACK = IXBACK + 1
C
               STORMBACK( STF_XB,IXBACK,IC ) =
     >                  POSITABL( X_POS, IBEF, IVOLBEF )
               STORMBACK( STF_YB,IXBACK,IC ) =
     >                  POSITABL ( Y_POS,IBEF,IVOLBEF )
C
C**  If need more past positions, update volume scan index and
C**  Correlation Table index.
C
               IF ( IXBACK .LT. ADPNPVOL )  THEN
                  IBEF = CORLTABL ( IBEF,IVOLBEF )
                  IVOLBEF = IVOLBEF - 1
                  IF (IVOLBEF .LT. 1) IVOLBEF = ADPNPVOL
                  GO TO 330
               ELSE
                  GO TO 1000
               END IF
            ELSE
C
C**  This is the last known position of current storm cell.  Save
C**  position.  Fill remaining positions (up to ADPNPVOL) with
C**  "Undefined".
C
               IXBACK = IXBACK + 1
C
               STORMBACK( STF_XB,IXBACK,IC ) =
     >                  POSITABL( X_POS,IBEF,IVOLBEF )
               STORMBACK( STF_YB,IXBACK,IC ) =
     >                  POSITABL( Y_POS,IBEF,IVOLBEF )
C
C**  No more previous positions exist for storm cell.  Set remaining
C**  past positions to "Undefined".
C
                  DO 400  NB = IXBACK + 1, ADPNPVOL
                     STORMBACK( STF_XB,NB,IC ) = UNDEF
                     STORMBACK( STF_YB,NB,IC ) = UNDEF
 400              CONTINUE
C
            END IF
C
         ELSE
C
C**  Else if Storm Type is "New", set all desired past positions to
C**  "Undefined".
C
            DO 600  NB = 1, ADPNPVOL
               STORMBACK( STF_XB,NB,IC ) = UNDEF
               STORMBACK( STF_YB,NB,IC ) = UNDEF
 600        CONTINUE
         END IF
C
 1000 CONTINUE
C
C*************** END OF PROCESSING FOR SUBROUTINE *******************
C
      RETURN
      END
