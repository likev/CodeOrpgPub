C
C RCS info
C $Author: christie $
C $Locker:  $
C $Date: 2002/12/16 20:36:08 $
C $Id: a31639.ftn,v 1.3 2002/12/16 20:36:08 christie Exp $
C $Revision: 1.3 $
C $State: Exp $
C
      SUBROUTINE A31639__FOR_TITLES(PAGE_NO,NUMSTORMS,TIVL,BUFOUT,
     >                              VOLNO,AVGSPEED,AVGDIREC)
*.********************************************************************
*.                    M O D U L E  P R O L O G U E
*.
*.  MODULE NAME: A31639__FOR_TITLES
*.
*.  MODULE VERSION: 0019
*.
*.  MODULE LANGUAGE: FORTRAN
*.
*.  CHANGE HISTORY:
*.
*.       DATE          VERSION   PROGRAMMER           NOTES
*.       ----------    -------   ----------------     ---------------
*.       10 25 85      0000      Mary Jo Staley       Part 2, RPG Build 3
*.       04 25 86      0001      MARY JO STALEY       SPR # 60118
*.       10 02 86      0002      J GROMMONS           SPR # 60346
*.       03 20 87      0003      JO STALEY            SPR # 70146
*.       08 22 88      0004      VALERIE HOLDEN       SPR # 80248
*.       05 09 89      0005      TERRI LEWIS          SPR # 80611
*.       01 09 90      0006      STACY RONALD         SPR # 90318
*.       07 05 90      0007      B. JOHNSTON          SPR # 90776
*.       01 30 91      0008      David M. Lynch       SPR # 91257
*.       12 03 91      0009      STEVE ANDERSON       SPR # 92740
*.       12/10/91      0010      ED NICHLAS           SPR 92637 PDL Removal
*.       04/24/92      0011      Toolset              SPR 91895
*.       03/25/93      0012      Toolset              SPR NA93-06801
*.       01/28/94      0013      Toolset              SPR NA94-01101
*.       03/03/94      0014      Toolset              SPR NA94-05501
*.       04/05/94      0015      Steve Smith          CCR 93034-001A00
*.       04/01/95      0016      Toolset              CCR NA95-11802
*.       04/30/95      0017      STEVE SMITH          CCR NA94-26302
*.       12/23/96      0018      Toolset              CCR NA95-11807
*.       03/16/99      0019      Toolset              CCR NA98-23803
*.
*.  CALLING SEQUENCE: CALL A31639__FOR_TITLES(PAGE_NO,NUMSTORMS,TIVL,
*.                                    BUFOUT,VOLNO,AVGSPEED,AVGDIREC)
*.
*.  MODULE FUNCTION:
*.
*.     This routine writes the title lines for the Storm Track
*.     tabular block into an internal file and then moves it to
*.     the product buffer.
*.
*.  MODULES CALLED: A3163C__MOVE_DATA, A3CM38__JULIAN2DATE 
*.
*.  PARAMETERS:          (*:  G = GLOBAL, C = COMMON, P = PASSED)
*.
*.    *   INPUT      TYPE        DESCRIPTION
*.    -   -----      ----        -----------
*.    G   SIRPGID    I*4         Site Adaptation - RPG ID Number.
*.    G   SUMMARY    I*4         Volume Scan Summary Table
*.    G   MPS_TO_KTS R*4         (CONSTANT)SCALING FACTOR FOR METERS PER
*.                               SECOND TO KNOTS
*.    G   SCAN_DATE  I*4         (CONSTANT)Volume Scan Date index into Scan
*.                               Summary Table
*.    G   SCAN_TIME  I*4         (CONSTANT)Volume Scan Time index into Scan
*.                               Summary Table
*.    P   AVGDIREC   R*4         Average Direc. of all "Continuing" storms,
*.                               this volume scan (degrees from)
*.    P   AVGSPEED   R*4         :Average Speed of all "Continuing" storms
*.    P   BUFOUT     I*2         The Product Output Buffer
*.    P   NUMSTORMS  I*4         NUMBER OF STORMS
*.    P   PAGE_NO    I*4         PAge Number of the Storm Track Tabular
*.                               Alphanumeric
*.    P   TIVL       I*4         TIME INTERVALS FOR FORCASTS.
*.    P   VOLNO      I*4         VOLUME SCAN NUMBER
*.
*.    *   OUTPUT  TYPE        DESCRIPTION
*.    -   ------  ----        -----------
*.    P   BUFOUT  I*2         The Product Output Buffer
*.
*.    *   ACTUAL ARGUMENTS  TYPE        DESCRIPTION
*.    -   ----------------  ----        -----------
*.    P   BUFOUT            I*2         The Product Output Buffer
*.
*.  DATABASE/FILE REFERENCE:  None
*.
*.  INTERNAL TABLES/WORK AREA:
*.
*.    NAME         TYPE        DESCRIPTION
*.    ----         ----        -----------
*.    BLKS         I*4         PARAMETER PRESET TO TWO BLANKS.
*.    CHARPOS1     I*2         Parameter = 18; Character position
*.    CHARPOS2     I*2         Parameter = 25; Character position
*.    CHARPOS3     I*2         Parameter = 51; Character position
*.    CHARPOS4     I*2         Parameter = 34; Character position
*.    CHARPOS5     I*2         Parameter = 47; Character position
*.    CHARPOS6     I*2         Parameter = 57; Character position
*.    CHARPOS7     I*2         Parameter = 76; Character position
*.    CLINE        CHAR*80     INTERNAL FILE FOR 6 LINES OF DATA
*.    FIRST_PAGE   I*4         Parameter value for first page of Storm Track
*.                             Tabular ALphanumeric
*.    I            I*4         Do loop index.
*.    I2S_PER_LINE I*4         NUMBER I*2 WORDS PER CHARACTER LINE
*.    IDATE        I*2         INTEGER FORMAT OF VOLUME SCAN DATE
*.    IDIR         I*4         Storm Cell direction to nearest integer value
*.    ILINE        I*2         EQUIV.TO CLINE FOR INTEGER
*.    INDX         I*4         Line index for the page
*.    INDX1        I*4         Parameter value for line number 1
*.    INDX2        I*4         Parameter value for second line number in title
*.    INDX4        I*4         Parameter value for the fourth line on the
*.                             first page
*.    INDX6        I*4         Parameter value for the sixth line
*.    ISPEED       I*4         Storm Cell Speed to nearest integer value in
*.                             knots
*.    ITIME        I*4         INTEGER REPRESENTATION OF VOLUME
*.    J            I*4         Loop control.
*.    NTLINES      I*4         NUMBER OF TITLE LINES
*.    SECPHR       I*4         Seconds per hour.
*.    SECPMIN      I*4         Seconds-per-minute.
*.    VDATE        I*2         VOLUME SCAN DATE
*.    VTIME        I*4         VOLUME SCAN TIME
*.
*.  GLOBAL BLOCKS REFERENCED:
*.
*.    A3CD05
*.    SITEADP
*.
*.  COMMON BLOCKS REFERENCED:
*.
*.
*.  ERROR CONDITIONS: NONE
*.
*.  ASSUMPTIONS/RESTRICTIONS:  None
*.
*.  DEVIATION FROM STANDARDS:  None
*.
*.  COMPILATION INSTRUCTIONS:
*.
*.        THIS MODULE IS COMPILED USING THE COMP16.CSS
*.
*.  LINKAGE INSTRUCTIONS:
*.
*.        THIS MODULE IS LINKED USING THE LINK16.CSS
*.
*.  MISC:  None
*.
*.*******************************************************************
      IMPLICIT NONE
C
C** INCLUDE SITE ADAPTATION DATA DEFINITIONS
C
$INCLUDE A309ADPT.INC/G,**A3CD70CA (NEND NLIST)
C
$INCLUDE A309.INC/G,**A3PM00 (NEND NLIST)
$INCLUDE            **A3PM06 (NEND NLIST)
$INCLUDE            **A3PM50 (NEND NLIST)
$INCLUDE            **A3CD05 (NEND NLIST)
C
      REAL AVGSPEED,AVGDIREC
      INTEGER*4 ISPEED,IDIR
C
      INTEGER INDX1,INDX2,INDX4,INDX6,INDX
      PARAMETER(INDX1=1,INDX2=2,INDX4=4,INDX6=6)
C
      INTEGER VOLNO
      INTEGER*2 VDATE ,IDATE(3)
C
      INTEGER*2 CHARPOS1, CHARPOS2, CHARPOS3, CHARPOS4
      INTEGER*2 CHARPOS5, CHARPOS6, CHARPOS7
      PARAMETER (CHARPOS1=17,CHARPOS2=20,CHARPOS3=46,CHARPOS4=29)
      PARAMETER (CHARPOS5=51,CHARPOS6=50,CHARPOS7=74)
C
      INTEGER VTIME
      INTEGER NUMSTORMS,TIVL(4),ITIME(3)
C
      INTEGER SECPHR,SECPMIN
      PARAMETER(SECPHR=3600,SECPMIN=60)
C
      INTEGER PAGE_NO,FIRST_PAGE,I,J,BLKS
      INTEGER NTLINES,I2S_PER_LINE
      PARAMETER(NTLINES=9,I2S_PER_LINE=40,FIRST_PAGE=1)
      PARAMETER(BLKS=X'2020')
C
      INTEGER*2 ILINE(40,NTLINES),BUFOUT(*)
      CHARACTER*80 CLINE(NTLINES)
      EQUIVALENCE(CLINE(1),ILINE(1,1))
C
C********************** E X E C U T A B L E ********************
C
C** INTIALIZE THE TITLE LINES TO BLANKS FIRST
C
      DO 10 I=1,NTLINES
         DO 20 J=1,I2S_PER_LINE
            ILINE(J,I)= BLKS
20       CONTINUE
10    CONTINUE
C
C** CONVERT THE DATE OF THE VOLUMN SCAN INTO FIELDS THAT CAN BE
C** WRITTEN INTO THE OUTPUT FILE FOR DISPLAY
C
      VDATE= SUMMARY(SCAN_DATE,VOLNO)
      VTIME= SUMMARY(SCAN_TIME,VOLNO)
C
      CALL A3CM38__JULIAN2DATE(VDATE,IDATE)
C
      ITIME(1)= VTIME/SECPHR
      ITIME(2)=(VTIME-ITIME(1)*SECPHR)/SECPMIN
      ITIME(3)=VTIME-ITIME(1)*SECPHR-ITIME(2)*SECPMIN
C
C** DATE COMES BACK AS  DAY,MON ,YR   FROM A3CM38
C
C** STORE THE FIRST TITLE LINE
C
      WRITE(CLINE(INDX1)(CHARPOS4:CHARPOS5),1)
C
C** SECOND TITLE LINE
C
      WRITE(CLINE(INDX2)(1:CHARPOS1),21) SIRPGID
C
      WRITE(CLINE(INDX2)(CHARPOS2:CHARPOS3),22) IDATE(2),IDATE(1),
     >                   IDATE(3),ITIME(1),ITIME(2),ITIME(3)
C
      WRITE(CLINE(INDX2)(CHARPOS6:CHARPOS7),23)NUMSTORMS
C
C** IF THIS IS THE FIRST PAGE OF THE PRODUCT, THEN...
C
      IF (PAGE_NO .EQ. FIRST_PAGE) THEN
C
C** CONVERT AVERAGE SPEED TO KTS.  BOTH SPEED AND DIRECTION TO
C** NEAREST INTEGER.
C
         ISPEED = NINT(AVGSPEED*MPS_TO_KTS)
         IDIR = NINT(AVGDIREC)
C
C** WRITE AVERAGE STORM SPEED AND DIRECTION
C
         WRITE(CLINE(INDX4),4) ISPEED, IDIR
         INDX = INDX6
      ELSE
         INDX = INDX4
      END IF
C
C** WRITE TITLE LINE
C
      WRITE(CLINE(INDX),6)
C
C** WRITE FORECAST INTERVAL LINE
C
      INDX = INDX + 1
      WRITE(CLINE(INDX),7) (TIVL(I),I=1,4)
C
C** WRITE TITLE LINE
C
      INDX = INDX + 1
      WRITE(CLINE(INDX),8)
C
C** MOVE THE DATA TO THE OUTPUT BUFFER
C
      DO 200 I=1,INDX+1
         CALL A3163C__MOVE_DATA(ILINE(1,I),BUFOUT)
200   CONTINUE
C
C** FORMAT STATEMENTS
C
 1    FORMAT('STORM POSITION/FORECAST')
 21   FORMAT(5X,'RADAR ID ',I3)
 22   FORMAT('DATE/TIME ',I2.2,':',I2.2,':',I2.2,'/',I2.2,':',I2.2,
     >       ':',I2.2)
 23   FORMAT('NUMBER OF STORM CELLS ',I3)
 4    FORMAT(19X,'AVG SPEED ',I2,1X,'KTS',4X,'AVG DIRECTION ',I3,1X,
     >       'DEG')
 6    FORMAT(1X,'STORM',4X,'CURRENT POSITION',14X,
     >       'FORECAST POSITIONS',15X,'ERROR')
 7    FORMAT(2X,'ID',5X,'AZRAN',5X,'MOVEMENT',4(4X,I2,' MIN'),
     >       4X,'FCST/MEAN')
 8    FORMAT(8X,'(DEG/NM)',2X,'(DEG/KTS)',3X,4('(DEG/NM)',2X),3X,
     >       '(NM)')
C
C** RETURN TO CALLER ROUTINE
C
      RETURN
      END
